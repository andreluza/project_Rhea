model {

# CAR prior - spatial random effect

for(j in 1:sumNeigh){weights[j] <- 1}
spacesigma ~ dunif(0,5)   #give prior for spacesigma
spacetau <- 1/(spacesigma*spacesigma)
alpha[1:nMuni] ~ car.normal(adj[],weights[],num[],spacetau)
  

### data model

for (i in 1:nMuni){   #loop over sites  #true occupancy(z)
    mu[i] <- alpha[i] + beta[1] + beta[2]*Cattleheads[i] + beta[3]*SoyaCover[i]
    mu.lim[i] <- min(10, max(-10, mu[i]))  
    logit(psi[i]) <- mu.lim[i] 
    z[i] ~ dbern(psi[i])
}

for (j in 1:nObs1){    #loop over observations - eBird - psi estimation
    e1[j] <- BETA[1]*eN[j] + BETA[2]*eHours[j] + BETA[3]*eKM[j]
    P1[j] <- 1-pow((1-0.5),e1[j])
    zP1[j] <- P1[j]*z[muni1[j]]
    Y1[j] ~ dbern(zP1[j])
}

for (k in 1:nObs2){    #loop over observations - wikiaves - psi estimation
    e2[k] <- BETA[4]*pic[k] 
    P2[k] <- 1-pow((1-0.5),e2[k])
    zP2[k] <- P2[k]*z[muni2[k]]
    Y2[k] ~ dbern(zP2[k])
}

beta[1] ~ dunif(-10,10)
beta[2] ~ dunif(-10,10)
beta[3] ~ dunif(-10,10)

for (a in 1:4){
  BETA[a] ~ dnorm(0,0.01)I(0,10000)
}
}
 